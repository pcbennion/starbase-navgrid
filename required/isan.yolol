A=1000 n=1000 pr=0 sp=1 dv=(9.6+2.4*pr)*n so=1-sp o=160000 e=8*o c=""
z="origin_" mr=z+"north" br=z+"south" cr=z+"east" z+="west" sm="\nS: "
u=e*3/13 l=2*o mm=l*13/19 k=e*3/19 :sy=4 q=15 x/=so sm=c :s=c
rr="ISAN2 :_\n     " xm="\nX: " ym="\nY: " zm="\nZ: " nn=l*13/3
ej=17*sp h="Q" pj=15-pr b=0.5 p=1000000 :at=mr :bt=br :ct=cr :dt=z x=0
t=(p-:a)^2 i=(p-:b)^2 g=(p-:c)^2 f=(p-:d)^2 :sy=jj++%4+4/(:sy>3) gotoq
h="M" t=(p-:a)^2 er=(t-el)/4 el=t x/=:a :at=br x=8 :sy=1 gotopj
i=(p-:a)^2 fr=(i-fl)/4 fl=i x/=:a :at=cr x=9 :sy=2 gotopj// ISAN From
g=(p-:a)^2 gr=(g-gl)/4 gl=g x/=:a :at=z x=10 :sy=3 gotopj// Collective
f=(p-:a)^2 hr=(f-hl)/4 hl=f x/=:a :at=mr x=7 :sy=0 gotopj// v2.5.3
x^=1 st=:_!=c :_=rr+"\n\n no signal\n " goto 11*(:a==0)*st
x^=1 st=:_!=c :_=rr+"\n\n can't see\n receivers" goto 12*st
st=:_!=c :_=rr+"\nadvanced chip required" goto 13*st
t+=er i+=fr g+=gr f+=hr //ISAN doc: isan.to/doc   -   Starmap: isan.to
x+=(6-x)*(x<7) :x=(t+i)/e+g/u-f/k :y=i/l-t/mm+(g+f)/nn :z=(g+f-t-i)/o
st=:_==c :_=rr+h+xm+:x/n*A+ym+:y/n*A+zm+:z/n*A+sm+:s gotoej+x*so+p*st
w+=:x uu+=:y vv+=:z r=w-tu j=uu-uv v=vv-vt ej+=(ii++%3)>1 gotox
m=(r*r+j*j+v*v)^b :s=m/dv*A tu=w uv=uu vt=vv w=0 uu=0 vv=0 ej=17 gotox
 // Modified for NavGrid https://github.com/pcbennion/starbase-navgrid
st=:_!=c :_=rr+"\n\n   ISAN2\n  OFFLINE" goto 20*st   // by StandPeter
// <-------------- this line is 70 characters long ------------------>

// isan.yolol
//     by Collective (GPLv3): isan.to/doc

// MODIFIED BY STANDPETER FOR USE BY NAVGRID:
// - Set speed mode to 1.
// - Save xx, yy, zz to :x, :y, :z respectively.
// - lines 6-10: Save a synchronization heartbeat to :sy to help keep
// all three ISAN chips in sync so they will all take readings from
// the same transmitter at the same time. This is necessary for
// Navgrid to work while moving. :sy is also used for damage detection
// and for displaying error messages.
// - lines 6-13: Use power operations where appropriate to make room
// for the :sy heartbeat
