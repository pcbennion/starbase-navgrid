ls="    |\n " ls2=ls+ls ld="----+----\n " :NavGrid="\n\n\nINIT" GOTO8
u=hm-(v<4)-mm-qm-u :NavGrid=u+yu-pt-bb-cc-dd-ee+t+yd-pt-zz-yy-xx-ww+di
pt=-:tPit-44 ht=:tHdg-44 pt=(pt<0)*pt+89 di=dm+:nDist v=:nStat+:sy
pt=(pt>0)*pt/18/s*s ht=(ht<0)*ht+89 ht=(ht>0)*ht/10/s*s ht+=As+(pt==2)
t=x-ht-a-b-c-d-e-f-g-h-i-j-k-l-m-n-o-p-q-r GOTO2+3*((v-:sy)==w)/(v<As)
ns=:nStat sy=:sy v=ns+sy bk=1/(v<As) GOTO6-(err--<1)
err+=err<10 IF ns=="" THEN em=sy ELSE sm=cr+ns END :NavGrid=em GOTO6
a="o   |    \n 0A0" b=" o  |    \n 1A0" c="  o |    \n 2A0" s=1000
d="   o|    \n 3A0" e="    o    \n 4A0" f="    |o   \n 5A0" nl="\n "
g="    | o  \n 6A0" h="    |  o \n 7A0" i="    |   o\n 8A0" mm=" M1"
j="o---+----\n 0A1" k="-o--+----\n 1A1" l="--o-+----\n 2A1" qm="Q 0"
m="---o+----\n 3A1" n="----o----\n 4A1" o="----+o---\n 5A1" hm=mm+qm
p="----+-o--\n 6A1" q="----+--o-\n 7A1" r="----+---o\n 8A1" dm="DST "
aa="0" bb=ls+"1" cc=ls2+"2" dd=ls2+ld+"3" ee=ls2+ld+ls+"4"  As="A"
zz=ls+ld+ls2+"0" yy=ld+ls2+"1" xx=ls2+"2" ww=ls+"3" vv="4"
x=a+b+c+d+e+f+g+h+i+j+k+l+m+n+o+p+q+r cr="\n\n\n" w=""
yu=nl+bb+cc+dd+ee yd=zz+yy+xx+ww GOTO3



// ------------------ This line is 70 chars long ------------------- >

// navgrid.yolol
//     by StandPeter

// ------------------ This line is 70 chars long ------------------- >

// Translates relative attitude to target into an ascii grid for
// visual navigation. Attitude is rounded to match a 5x9
// crosshair-style grid due to limitations imposed by the Text Panel
// display.

// Also displays:
// - Whether ISAN is using MONO ("M") or QUAD ("Q"), in the top right.
// - Distance to target, at the bottom.
// - Messages for NavP or ISAN errors